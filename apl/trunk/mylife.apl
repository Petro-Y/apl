⍝ TODO: кешування (інтегрувати в rep або play)....
hash←{256|⌈{⍵+○⍺}/,⍵}
cache←256 2⍴0
cacheindex←256⍴⊂⍬ ⍝ хеш-таблиця, що містить списки захешованих ⍺ для кожної ⍵ ....
cached←{i←hash ⍺,⍵⋄ cache[i;0]≡⍺ ⍵:cache[i;1]⋄ res←⍺⍶⍵⋄ cache[i;0]←⍺ ⍵⋄ cache[i;1]←res⋄ res}

⍝⍝ ⍟⍟⍟⍟⍟⍟⍟⍟⍟⍟⍟ CONWAY'S GAME OF LIFE ⍟⍟⍟⍟⍟⍟⍟⍟⍟⍟⍟⍟⍟⍟ ⍝⍝
_life←{((⍵×9)+⊃+/+/¯1 0 1∘.⊖¯1 0 1∘.⌽⊂⍵)∊⍺}
life←{12 13 3 _life ⍵}
mylife←{((⍵×9)+⊃+/+/¯1 0 1∘.⊖¯1 0 1∘.⌽⊂⍵)∊12 13 3}

⍝ NGN APL:
zborder← ({⍉⊖{⌈/0⌷⍵:(-1 0+⍴⍵)↑⍵⋄⍵}{⌈/0⌷⍵:⍵⋄1↓⍵}⍵}⍣4){(3 3⌈(⍴⍵))↑⍵}
⍝                      запобігти помилці порожнього поля⍝   
inf←{⍶ zborder ⍵} 
__play←{map←⍵ ⋄ ⍶{⊂'.≢'[(⍶⍣⍵) map]}¨⍺}
_play←{map←⍵ ⋄ ⍶{⊂'.≢'[zborder (⍶⍣⍵) map]}¨⍺}
inflife←mylife zborder
play←{map←⍵ ⋄ {⊂'.≢'[zborder (inflife⍣⍵) map]}¨⍺}

⍝ NARS2000:
⎕io←0
rep←{⍺>0:⍺⍺((⍺-1)⍺⍺ ∇∇ ⍵)⋄ ⍵} ⍝ замінник відсутнього в цій реалізації оператора ⍣
⍝ N f rep A ←→ (f⍣N)A
zborder←{4 {⍉⊖{(- (↑⌈/⍵) 0+⍴⍵)↑⍵}{(~↑⌈/⍵) 0↓⍵}⍵} rep (3 3⌈(⍴⍵))↑⍵}
inflife←{mylife zborder ⍵}
play←{map←⍵ ⋄ {⊂'∘≢'[zborder ⍵ inflife rep map]}¨⍺}

⍝ GNU APL:
⎕io←0
zborder←{({⍉⊖{(- (↑⌈/⍵) 0+⍴⍵)↑⍵}{(~↑⌈/⍵) 0↓⍵}⍵}⍣4) ⍵}
inflife←{mylife zborder ⍵}
play←{(⊂⍵)∘.{⊂'.≢'[zborder (inflife⍣⍵) ⊃⍺]}⍺}


⍝ перетворення з форматів:
bitmap←{⍺⍴⍉(16⍴2)⊤⍵}

⍝ конструктори фігур:
over←{max←(⍴⍺)⌈⍴⍵⋄(max↑⍺)⍶max↑⍵}
join←∨over
without←∧∘~over
shift←{(- ⍺+⍴⍵)↑⍵}
grow←{(⍺+⍴⍵)↑⍵}

symmv←⊢⍪⊖
symmh←⊢,⌽
symm4←symmv symmh
oddv←{(y x)←(⍴⍵)÷¯2 ⋄ y⊖1 0↓y⊖⍵}
oddh←{(y x)←(⍴⍵)÷¯2 ⋄ x⌽0 1↓x⌽⍵}
odd4←{(y x)←(⍴⍵)÷¯2 ⋄ x⌽y⊖1 1↓x⌽y⊖⍵}
oddsymmv←oddv symmv
oddsymmh←oddh symmh
oddsymm4←odd4 symm4
symmd←⍉join⊢
dsymm←⊢∨(⌽⊖)
dsymmv←⌽⍪⊖
dsymmh←⊖,⌽
rsymm←{dsymmv(⊢,(⍉⊖))(¯1 1×(⌽⍴⍵)⌈⍴⍵)↑⍵}
stair←{(n y x)←⍺ ⋄ n≤1:⍵ ⋄ ⍵ join y x shift (n-1) y x∇⍵}

eight←{{(⍉⍵),⍉⌽⍵}(⍵ ⍵⍴1),⍵ ⍵⍴0}
mkship←{⊃(3⍴1)(1 0 0 1),(⍵⍴1),⊂(0 1)}⍝ 2≤⍵≤4
pyramid←{oddsymmv⊃1⍴⍨¨1+⍳⍵}
diag←{⍵ ⍵⍴1,⍵⍴0}

⍝ допоміжні прийоми:
⍝{(2 1÷⍨⍴⍵)↑⍵}¨(H W⍴N+⍳99) play SYMMETRIC ⍝ обрізка симетричного об'єкта (де нижня частина є відображенням верхньої)
halfv←{(⌈2 1÷⍨⍴⍵)↑⍵}¨

⍝ TODO: brief - ф-я для скороченого відображення завеликих фігур.....
gliderL←⊃1(1 0 1)(1 1)
gliderY←⊃(1 0 1)(1 1)(0 1)
⍝ kickgliders - виявити й стерти втікаючі глайдери, додавши інформацію про їх кількість (і напрям)....
	⍝ gliderL ⍷ map - пошук матриці в матриці (повторити для всіх глайдерів і всіх розворотів)....
	⍝(,glider⍷map)/,⍳⍴map ⍝ - результат пошуку в вигляді списку вкладених пар координат
	⍝ відкинути ті з них, які ні з чим не можуть перетнутися (проблема швидших об’єктів — ?).....
	⍝ оформити результат.....
⍝ відображення половини або чверті для симетричних фігур (з інформацією про тип симетрії, парність/непарність)....
	⍝ позначення країв у залежності від типу зрізу:
	⍝ ...↑ oddsymmh
	⍝ ...↑
	⍝ ...↑
	⍝ ←←←  oddsymmv

	⍝ ...|↑
	⍝ ...|↑ symmh
	⍝ ...|↑
	⍝ ---+ 
	⍝ ←←←  symmv

	⍝ ...*
	⍝ ...*
	⍝ ...*
	⍝ **** odd4 rsymmv (якщо з обрамленням лінією — без odd4)

	⍝ ...% oddh dsymmh
	⍝ ...%
	⍝ ...%
	⍝ %%%  oddv dsymmv
⍝ Або так:	
	⍝ ...║
	⍝ ...⌽
	⍝ ...║ (горизонтальна/вертикальна симетрія)
	⍝ ═⊖═╝	
	
	⍝ ...│
	⍝ ...⌽ (те ж саме з непарною віссю)
	⍝ ...│
	⍝ ─⊖─┘
	
	⍝ ...║
	⍝ ...* (радіальна симетрія)
	⍝ ...║
	⍝ ═*═* 	
	
	⍝ ...║
	⍝ ...⍉ (точкова симетрія)
	⍝ ...║
	⍝ ─⍉──
⍝ стирати великі пустоти, наповнюючи їх лініями ↔ чи ↕ з указаним розміром проміжку......

⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝
⍝ Деякі красиві форми:
⍝{⍵{{'.≡'[⍵]}zborder(inflife⍣⍺) ⍵}(10 10⍴⍳100)∊11 12 22 23 33 34}¨⍳8 
      ⍝ randmap←?5 5⍴2
⍝ 0 1 1 1 1
⍝ 1 0 0 1 1
⍝ 0 1 0 0 0
⍝ 1 1 1 1 1
⍝ 0 0 1 1 0

      ⍝ ⍉randmap3 дуже схожа
⍝ 1 0 0 1
⍝ 1 1 1 0

      ⍝ randmap4←?2 5⍴2
⍝ 1 0 0 1 0
⍝ 1 0 1 1 1

⍝ digit8←{(⍉⍵),⍉⌽⍵}(3 3⍴1),3 3⍴0
ship←5 4⍴0 1 1 1, 1 0 0 1, 0 0 0 1,0 0 0 1, 0 0 1 0
⍝4 3⍴ 0 1 0, 1 1 1

⍝ ⊃ (1 1 1) (1 1)1 1

⍝ варіант із журналу:
⍝cannon←⊃(0 0 0 0 1)(0 0 0 0 1 1) 0 0 0 0 0 0 0 0(0 0 0 0 1 1 1)(0 0 0 1 0 0 0 1)(0 0 1 0 0 0 0 0 1)(0 0 1 0 0 0 0 0 1)0 0 0 0 0 0 0 0 0(0 1 0 0 0 0 0 1) (1 1 0 0 0 0 0 1 1)0(0 0 0 1 1 1)(0 0 0 1 1 1)(0 0 0 0 1)0 0 0 0 0(0 0 0 1)(0 0 0 1 1)
⍝ (десь помилка!)

⍝ https://en.wikipedia.org/wiki/Gun_(cellular_automaton)
⍝ https://upload.wikimedia.org/wikipedia/commons/e/e0/Game_of_life_glider_gun.svg
gun←⊃(0 0 0 0 1 1)(0 0 0 0 1 1)0 0 0 0 0 0 0 0(0 0 0 1 1 1)(0 0 1 0 0 0 1)(0 1 0 0 0 0 0 1)(0 1 0 0 0 0 0 1)(0 0 0 0 1)(0 0 1 0 0 0 1)(0 0 0 1 1 1)(0 0 0 0 1) 0 0 (0 0 0 0 0 1 1 1)(0 0 0 0 0 1 1 1)(0 0 0 0 1 0 0 0 1)0(0 0 0 1 1 0 0 0 1 1)0 0 0 0 0 0 0 0 0(0 0 0 0 0 0 1 1)(0 0 0 0 0 0 1 1)

k←⊃(1 0 1)(1 1)(1 0 1)
⍝ 1 10⍴1 ⍝ після двох ходів перетворюється на осцилятор з періодом 15
prepulsar←⊃9⍴6↑3⍴(1 1 1)(1 0 1)
prepulsar1←⊃0(11⍴6↑5⍴1)

clock←⊃(0 0 1)(1 1)(0 0 1 1)(0 1)
biclock←clock join (-3 3+⍴clock)↑clock

⍝ ⊃(⊢,⌽)(1 1)(1 1)0(1 1 0 1 1)
queenbee←⊃(1 1 0 0 0 1 1)(0 0 1 1 1)(0 1 0 0 0 1)(0 0 1 0 1)(0 0 0 1)


 

⍝ (11 3⍴+⍳99) play ⊃(0 0 1)0(1 0 1 1)(1 1 0 1)0 (0 1)
⍝ (14 4⍴+⍳99) play ⊃0(1 1 1 0 1 1 1 1 1 0 1 1 1)
⍝ 4 4⍴(16⍴2)⊤8437 ⍝ або 8446
⍝  4 4⍴(16⍴2)⊤0xC777
⍝ (16 3⍴+⍳99)play 2 4 1 stair 1 5⍴1
⍝ (20 3⍴+⍳99)play 6 2 0 stair 2 1 2 stair 1 2⍴1
	⍝ на закільцьованому полі перетворюється на осцилятор з періодом 50:
	⍝ (17 3⍴16+⍳99)mylife __play ¯10⌽3 20 grow 6 2 0 stair 2 1 2 stair 1 2⍴1 
	⍝ (17 1 3⍴16+⍳99)mylife __play ¯1⊖¯10⌽14 24↑12 4⍴2⌽8↑4⍴1
	⍝ (traffic jam reaction)
⍝ 10 2 0 stair 2 1 2 stair 1 2⍴1
⍝  2 1 5 stair 1 5⍴1
⍝ (13 3⍴⍳99)play dsymmh ⊃0 (8⍴1)
⍝ dsymmh ⊃0 (10⍴1)
⍝ (5 5⍴⍳99) play oddsymmv⊃1⍴⍨¨1+⍳4
⍝ oddsymmv⊃1⍴⍨¨1+⍳7 ⍝ утворює quenbee?
⍝ 2 2↓eight 6 ⍝ перевірити, чи відбувається  взаємодія в області діагоналі на пізніх стадіях....
⍝ (якщо фігура має діагональну симетрію і діагональ порожня, то народження на самій лінії діагоналі 
⍝ стає неможливим, оск. до її клітинок завжди торкається парна кількість сусідів!
⍝ Узагалі, будь-яка непарна порожня вісь двобічної симетрії стає незаповнюваною (а отже, нездоланною перешкодою).

⍝ (5 2⍴20+⍳99)play(symmd⊃(0 0 1)(0 1 0 1 1 0)0 0 0(0 0 0 1 1 1))join 6 6 shift (diag 35) join 35 34 shift 2 2⍴0 1 1 1 ⍝ fuse